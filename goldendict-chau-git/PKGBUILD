# Maintainer: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: m0rph <m0rph.mailbox@gmail.com>
pkgname=goldendict-chau-git
pkgver=r2445.8d54db7
pkgrel=1
pkgdesc="Feature-rich dictionary lookup program"
arch=('x86_64')
url="http://goldendict.org"
license=('GPL3')
depends=(libvorbis libogg hunspell libxtst libzip libao qt5-webkit qt5-svg
         qt5-x11extras qt5-tools phonon-qt5 ffmpeg)
makedepends=(git)
provides=(stardict goldendict)
conflicts=(goldendict)
source=($pkgname::git+https://github.com/duongdominhchau/goldendict.git)
sha256sums=(SKIP)

pkgver() {
    cd "${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd ${pkgname}
    qmake-qt5 "CONFIG+=no_epwing_support" PREFIX="/usr" goldendict.pro
    make
}

package() {
    cd ${pkgname}
    make INSTALL_ROOT="${pkgdir}" install
    # languages
    install -Dm644 -t "${pkgdir}/usr/share/goldendict/locale" locale/*.qm 
    # removing unneeded
    rm -rf "${pkgdir}/usr/share/app-install"
}

